// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
        
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath 'com.google.gms:google-services:4.3.3'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.0.0-beta02'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

ext {
    appcompatVersion = '[1.1.0,2.0.0['
    veridVersion = '[1.16.0,2.0.0['
    rxveridVersion = '[1.6,2.0['
    blinkidVersion = '[5.0.0,6.0.0['
    veriduielementsVersion = '[1.0.0,2.0.0['
    versionMajor = 7
    versionMinor = 5
    versionPatch = 1
    versionClassifier = null
    versionClassifierVersion = ""
    minSdkVersion = 23
}

Integer generateVersionCode() {
    return ext.minSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier + ".${ext.versionClassifierVersion}"
    }
    return versionName
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://dev.ver-id.com/artifactory/gradle-release' }
        maven { url 'http://maven.microblink.com' }
        maven { url 'https://mvnrepository.com/artifact/org.apache.commons/commons-math3' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
